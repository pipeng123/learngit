# Sample Linker Command File for CodeWarrior for ColdFire MCF51JM128

# Memory ranges

MEMORY {

   bootloader  (RX)  : ORIGIN = 0x0001D800, LENGTH = 0x00002800
   bootvector  (RWX) : ORIGIN = 0x00800000, LENGTH = 0x000001BC
   userram     (RWX) : ORIGIN = 0x00800200, LENGTH = 0x00003E40
   
   
}

SECTIONS {


    __IPSBAR  =  0xFFFF8000;
 
    __VECTOR_TABLE_BASE = 0;
    
    DBUFFER_SIZE = 255;
   

# Heap and Stack sizes definition
  ___heap_size     = 0x000;
  ___stack_size    = 0x0200;

# MCF51JM128 Derivative Memory map definitions from linker command files:
# ___RAM_ADDRESS, ___RAM_SIZE, ___FLASH_ADDRESS, ___FLASH_SIZE linker
# symbols must be defined in the linker command file.

# 16 Kbytes Internal SRAM
   ___RAM_ADDRESS = 0x00800000;
   ___RAM_SIZE    = 0x00004000;

# 128 KByte Internal Flash Memory
   ___FLASH_ADDRESS  = 0x00000000;
   ___FLASH_SIZE     = 0x00020000;

  .userram        : {} > userram
  .bootloader     : {} > bootloader

  
  .text :
  {
 
    *(.text)
    . = ALIGN (0x4);
    
    *(.rodata)
    . = ALIGN (0x4);
  
    ___ROM_AT = .;
  
    ___DATA_ROM = .;
  
   } >> bootloader
    


  .data : AT(___ROM_AT)
  {
    ___DATA_RAM = .;
    . = ALIGN(0x4);
    *(.exception)
    . = ALIGN(0x4);
    __exception_table_start__ = .;
    EXCEPTION
    __exception_table_end__ = .;
    ___sinit__ = .;
      STATICINIT
    __START_DATA = .;

    *(.data)
    . = ALIGN (0x4);
    __END_DATA = .;

    __START_SDATA = .;
    *(.sdata)
    . = ALIGN (0x4);
    __END_SDATA = .;

    ___DATA_END = .;
    __SDA_BASE = .;
    
    . = ALIGN (512);
   
  } >> userram

  .bss :
  {
     . = ALIGN (0x4);
    ___BSS_START = .;
    __START_SBSS = .;
    _dbuffer = .;    // to align dbuffer to longword address
    .  = . + DBUFFER_SIZE;
    # . = ALIGN(4); 
    *(.sbss)
    . = ALIGN (0x4);
    *(SCOMMON)
    __END_SBSS = .;

    __START_BSS = .;
    *(.bss)
    . = ALIGN (0x4);
    *(COMMON)
    __END_BSS = .;
    ___BSS_END = .;
      
    . = ALIGN (512);

  } >> userram

  .custom :
  {
    ___HEAP_START       = .;
    ___heap_addr        = ___HEAP_START;
    ___HEAP_END         = ___HEAP_START + ___heap_size;
    ___SP_END             = ___HEAP_END;
    ___SP_INIT          = ___SP_END + ___stack_size;
   . = ___SP_INIT;

 #   . = ALIGN (0x4);
         /* Buffer descriptor base address
           shall be aligned to 512 byte boundary.
           Size shall be 512 bytes. */
        .				= ALIGN(512);
        __BDT_BASE		= .;
        .               = . + 512;
        __BDT_END       = .;

    . = ALIGN (512);

  } >> userram



  __SP_INIT             = ___SP_INIT;

  ___SP_AFTER_RESET     = __SP_INIT;

  _romp_at = ___ROM_AT + SIZEOF(.data);
  .romp : AT(_romp_at)
  {
    __S_romp = _romp_at;
    WRITEW(___ROM_AT);
    WRITEW(ADDR(.data));
    WRITEW(SIZEOF(.data));
    WRITEW(0);
    WRITEW(0);
    WRITEW(0);
  }
}
